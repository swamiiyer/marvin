# Accepts n (int) from standard input and writes n! (computed recursively) to standard output.

0     read      r0            # read n
1     pushr     r0 r15        # mem[sp++] = n
2     calln     r12 6         # n! = factorial(n)
3     addn      r15 -1        # sp = sp - 1
4     write     r13           # write n!
5     halt                    # halt the machine

# int factorial(int n):
#   input : r0 = n
#   output: r13 = n!
#   temps: r1, r2

# Save ra and fp, and set fp to sp.
6     pushr     r12 r15       # mem[sp++] = ra
7     pushr     r14 r15       # mem[sp++] = fp
8     copy      r14 r15       # fp = sp

# Save registers used.
9     pushr     r0 r15        # mem[sp++] = r0
10    pushr     r1 r15        # mem[sp++] = r1
11    pushr     r2 r15        # mem[sp++] = r2

12    loadn     r0 r14 -3     # n = mem[fp - 3]
13    jnezn     r0 16         # if n != 0 jump to 16 (recursive step),
                              # else fall through (base case)

# Base case.
14    setn      r13 1         # output = 1
15    jumpn     22            # jump to 22

# Recursive step.
16    copy      r2 r0         # r2 = n
17    addn      r2 -1         # n = n - 1
18    pushr     r2 r15        # mem[sp++] = n - 1
19    calln     r12 6         # (n-1)! = factorialRec(n-1)
20    addn      r15 -1        # sp = sp - 1
21    mul       r13 r0 r13    # n! = n(n-1)!

# Restore registers used.
22    popr      r2 r15        # r2 = mem[--r15]
23    popr      r1 r15        # r1 = mem[--r15]
24    popr      r0 r15        # r0 = mem[--r15]

# Restore fp and ra, and jump to ra (caller).
25    popr      r14 r15       # fp = mem[--r15]
26    popr      r12 r15       # ra = mem[--r15]
27    jumpr     r12           # jump to caller
